apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {

        applicationId "com.wardellbagby.tokipona"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "ROLLBAR_ACCESS_TOKEN", "\"$System.env.ROLLBAR_ACCESS_TOKEN\""
        buildConfigField "Boolean", "CRASH_REPORTING_ENABLED", "$System.env.CRASH_REPORTING_ENABLED"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.9.1'
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.android.support:support-v4:$support_lib_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:support-vector-drawable:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.bowyer.app:fab-toolbar:1.1.0@aar'
    implementation 'org.droidparts:droidparts-misc:3.2.0'
    implementation 'com.github.yamamotoj:pikkel:0.3.3'
    //noinspection GradleDependency This is the last version that we can use until minSdk is 24+ or Java 8 support gets better in Android.
    implementation 'org.apache.opennlp:opennlp-tools:1.7.2'
    implementation "io.reactivex.rxjava2:rxkotlin:$rx_version"
    implementation "io.reactivex.rxjava2:rxjava:$rx_version"
    implementation 'com.rollbar:rollbar-android:0.2.1'
    implementation 'io.mattcarroll.hover:hover:0.9.8'
    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation('com.github.ozodrukh:CirculaRreveal:2.0.1') {
        transitive = true
    }
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.8.47"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}